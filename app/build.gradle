apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.alirezaafkar.phuzei"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 220
        versionName "2.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())

    flavorDimensions "version"
    productFlavors {
        free {
            dimension "version"
            resValue "string", "app_name", "Phuzei"
            buildConfigField "boolean", "IS_PRO", "false"

            def client_id = properties.getProperty('client_id_free')
            buildConfigField 'String', 'CLIENT_ID', "${client_id}"

            def phuzeiAuthorityValue = "com.alirezaafkar.phuzei"
            manifestPlaceholders = [phuzeiAuthority: phuzeiAuthorityValue]
            buildConfigField "String", "PHUZEI_AUTHORITY", "\"${phuzeiAuthorityValue}\""
        }
        pro {
            dimension "version"
            applicationIdSuffix ".pro"
            resValue "string", "app_name", "Phuzei Pro"
            buildConfigField "boolean", "IS_PRO", "true"

            def client_id = properties.getProperty('client_id_pro')
            buildConfigField 'String', 'CLIENT_ID', "${client_id}"

            def phuzeiAuthorityValue = "com.alirezaafkar.phuzei.pro"
            manifestPlaceholders = [phuzeiAuthority: phuzeiAuthorityValue]
            buildConfigField "String", "PHUZEI_AUTHORITY", "\"${phuzeiAuthorityValue}\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true

            proguardFile "$project.rootDir/app/proguard/retrofit2.pro"
            proguardFile "$project.rootDir/app/proguard/androidx.pro"
            proguardFile "$project.rootDir/app/proguard/okhttp.pro"
            proguardFile "$project.rootDir/app/proguard/kotlin.pro"
            proguardFile "$project.rootDir/app/proguard/gson.pro"
            proguardFile "$project.rootDir/app/proguard/app.pro"
        }
    }
}

ext {
    dagger_version = '2.27'
    okHttp_version = '4.7.2'
    retrofit_version = '2.9.0'
    lifecycle_version = "2.2.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    kapt 'com.google.dagger:dagger-compiler:' + dagger_version

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.work:work-runtime-ktx:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'androidx.core:core-ktx:1.3.1'

    implementation 'com.google.dagger:dagger:' + dagger_version
    implementation 'com.squareup.okhttp3:okhttp:' + okHttp_version
    implementation 'com.squareup.retrofit2:retrofit:' + retrofit_version
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + kotlin_version
    implementation 'com.squareup.retrofit2:converter-gson:' + retrofit_version
    implementation 'com.squareup.retrofit2:adapter-rxjava2:' + retrofit_version
    implementation 'com.squareup.okhttp3:logging-interceptor:' + okHttp_version
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:' + lifecycle_version
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:' + lifecycle_version
}
